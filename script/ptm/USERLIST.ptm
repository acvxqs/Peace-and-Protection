script\userlist.mrc
; #= P&P -rs
; ########################################
; Peace and Protection
; Userlist support, editing, etc.
; ########################################

;
; Popup preliminary
;
alias _addrpop {
  if ($address($remove($active,=),5)) set -u %.targaddr $ifmatch
  elseif (($query($active)) && ($query($active).address != $active)) set -u %.targaddr $active $+ ! $+ $ifmatch
  else unset %.targaddr
}

; Finds first matching user in the userlist, if any
; $_fuser(fulladdress) / $_fuser2(maddress) finds iswm both ways
alias _fuser var %num = 1 | :loop | if ($ulist(*,%num)) { if ($ifmatch iswm $1) return $ifmatch | inc %num | goto loop }
alias _fuser2 var %num = 1 | :loop | if ($ulist(*,%num)) { if ($ifmatch iswm $1) return $ifmatch | if ($1 iswm $ulist(*,%num)) return $ulist(*,%num) | inc %num | goto loop }

;
; Userlist
;

; /user [-v] address[,addr...]
; /user -r address[,addr...] [#chan]
; /user address[,addr...] [#chan|*] [-r|lvl] [x|-|+|%|@ [pw|?|??|!]] [=nick] [<lvl] [>lvl] [[nn]]

; address can be a nick, fulladdress, mask, etc.

; No options- Views/edits userlist entry
;   -r to remove entry, or with #chan to remove that channel
;   -v to view entry (text)

; Manual editing- (options in any order, even repeated)
;   #chan allows channel-specific levels
;   lvl is numeric level, or -r to remove that channel
;   x/-/+/%/@ is op status
;   pw is password, ? for required but not set, ! for automatic, ?? for required but not set but left alone if already set
;   nick allow modification of AKA
;   can use >lvl to manually add a custom named level, and <lvl to remove
;   ^k removes or adds nicklist color level =colorXX

alias user {
  if ($1 == $null) _qhelp /user

  var %flag,%mask,%rest,%stuff,%ule-mask,%ule-raw,%ule-nick,%num,%chan,%newlvl,%tok,%pw

  if (-* iswm $1) { %flag = $1 | %mask = $_nc($gettok($2,1,44)) | %rest = $gettok($2,2-,44) | %stuff = $3- }
  else { %flag = - | %mask = $_nc($gettok($1,1,44)) | %rest = $gettok($1,2-,44) | %stuff = $2- }

  %ule-mask = $_ppmask(%mask,$_stmask(3),1)
  if (%ule-mask == $null) {
    dispa [phrase:address_lookup:nick=$;t(%mask)]
    _notconnected
    _Q.userhost user $+ %flag $+ &n!&a, $+ %rest $+  $+ $iif(%stuff,$_s2p(%stuff)) dispa[error:no_user:nick=$;t(%mask):s2p]. %mask
    halt
  }
  %ule-raw = $level(%ule-mask)
  %ule-nick = $readini($_cfg(userinfo.ini),n,%ule-mask,nick)
  if (%ule-nick == $null) {
    if ((* !isin %mask) && (! !isin %mask) && (@ !isin %mask) && (. !isin %mask)) %ule-nick = %mask
    if ((! isin %mask) && (* !isin $gettok(%mask,1,33))) %ule-nick = $gettok(%mask,1,33)
  }

  if (%flag == -v) {
    _info /user -v
    dispr @Info [userlist:display:address=$;t(%ule-mask)] $iif(%ule-nick,[userlist:aka:nick=$;s(%ule-nick)]) -
    %num = $numtok(%ule-raw,44)
    :view
    if ($_namedul($gettok(%ule-raw,%num,44),%ule-mask)) dispr @Info   - $ifmatch
    if (%num > 1) { dec %num | goto view }
    if (%rest) _juryrig2 user %flag %rest %stuff
    return
  }
  if (%flag == -r) {
    _info /user -r
    if (%ule-raw == $dlevel) { .ruser %ule-mask | dispr @Info [userlist:not_in_list:address=$;t(%ule-mask)] }
    elseif (%stuff) {
      %chan = $gettok(%stuff,1,32)
      if (($left(%chan,1) !isin & $+ $remove($chantypes,+)) && (%chan != *)) dispr @Info [userlist:not_a_chan:chan=$;t(%chan)]
      elseif ((%chan != *) && (,= $+ %chan $+ Â¬ !isin %ule-raw)) disptc @Info %chan [userlist:not_in_chan:address=$;t(%ule-mask)]
      else {
        remini $_cfg(userinfo.ini) %ule-mask %chan
        %ule-raw = $_leveledit(%chan,%ule-raw)
        if ((Â¬ !isin %ule-raw) && (%chan == *) && ($wildtok(%ule-raw,=color*,1,44))) %ule-raw = $remtok(%ule-raw,$ifmatch,1,44)
        if (%ule-raw == $dlevel) {
          remini $_cfg(userinfo.ini) %ule-mask
          .ruser %ule-mask
        }
        else .auser %ule-raw %ule-mask
        disptc @Info %chan [userlist:chan_remove:address=$;t(%ule-mask)]
      }
    }
    else {
      remini $_cfg(userinfo.ini) %ule-mask
      .ruser %ule-mask
      dispr @Info [userlist:user_remove:address=$;t(%ule-mask)]
    }
    scon -at1 _nickcol.updatemask %ule-mask 1
    if (%rest) _juryrig2 user -r %rest %stuff
    return
  }
  if (%stuff) {
    _info /user
    dispr @Info [userlist:editing:address=$;t(%ule-mask)] $iif(%ule-nick,[userlist:aka:nick=$;s(%ule-nick)]) -
    %chan = *
    %newlvl = $_level(*,%ule-raw)
    if (%rest) _juryrig2 user %rest %stuff
    :edit
    %tok = $gettok(%stuff,1,32)
    %stuff = $gettok(%stuff,2-,32)
    ; [#chan|*] [-r|lvl] [x|-|+|%|@ [pw|?|??|!]] [=nick] [<lvl] [>lvl] [[nn]]
    if ($istok(x.-.+.%.@,%tok,46)) {
      %newlvl = $calc(%newlvl) $+ $iif(%tok != x,%tok)
      if (%tok isin x-) remini $_cfg(userinfo.ini) %ule-mask %chan
      else {
        %pw = $gettok(%stuff,1,32)
        if (($left(%pw,1) isletter) || ($istok(!.?.??,%pw,46))) {
          if (%pw == ??) {
            %pw = $readini($_cfg(userinfo.ini),n,%ule-mask,%chan)
            if (%pw == $null) %pw = ?
          }
          if (%pw == !) remini $_cfg(userinfo.ini) %ule-mask %chan
          else writeini $_cfg(userinfo.ini) %ule-mask %chan $iif(%pw == ?,?,$hash(%pw,32))
        }
      }
      %pw = $readini($_cfg(userinfo.ini),n,%ule-mask,%chan)
      if (%tok isin x-) var %pw
      elseif (%pw == ?) %pw = ([userlist:pw_unset:lower])
      elseif (%pw == $null) %pw = ([userlist:pw_auto:lower])
      else %pw = ([userlist:pw_set:lower])
      dispr @Info   - $iif(%chan == *,[word:default],$:s(%chan)) [word:status] - $gettok([userlist:mode_none]*[userlist:mode_deop]*[userlist:mode_voice]*[userlist:mode_hop]*[userlist:mode_op],$pos(x-+%@,%tok),42) %pw
    }
    elseif (%tok == -r) {
      remini $_cfg(userinfo.ini) %ule-mask %chan
      dispr @Info   - $iif(%chan == *,[word:default],$:s(%chan)) [userlist:cleared]
      var %newlvl
    }
    elseif ($left(%tok,1) isin & $+ $remove($chantypes,+)) {
      %ule-raw = $_leveledit(%chan,%ule-raw,%newlvl)
      %chan = %tok
      %newlvl = $_level(%chan,%ule-raw)
    }
    elseif (%tok == *) {
      %ule-raw = $_leveledit(%chan,%ule-raw,%newlvl)
      %chan = *
      %newlvl = $_level(*,%ule-raw)
    }
    elseif (=* iswm %tok) {
      if (%tok == =) {
        dispr @Info   - [userlist:nick_cleared]
        var %ule-nick
        remini $_cfg(userinfo.ini) %ule-mask nick
      }
      else {
        dispr @Info   - [userlist:nick_stored:nick=$;t($right(%tok,-1))]
        %ule-nick = $right(%tok,-1)
      }
    }
    elseif (%tok isnum) {
      %newlvl = %tok $+ $remove(%newlvl,$calc(%newlvl))
      dispr @Info   - $iif(%chan == *,[word:default],$:s(%chan)) [userlist:level_set:num=$;t(%tok)]
    }
    elseif (>* iswm %tok) {
      %tok = $right(%tok,-1)
      if (=* !iswm %tok) %tok = = $+ %tok
      %ule-raw = $addtok(%ule-raw,%tok,44)
      dispr @Info   - [userlist:custom_add:level=$;t(%tok)]
    }
    elseif (<* iswm %tok) {
      %tok = $right(%tok,-1)
      if (=* !iswm %tok) %tok = = $+ %tok
      %ule-raw = $remtok(%ule-raw,%tok,44)
      dispr @Info   - [userlist:custom_remove:level=$;t(%tok)]
    }
    elseif (* iswm %tok) {
      if ($wildtok(%ule-raw,=color*,1,44)) %ule-raw = $remtok(%ule-raw,$ifmatch,1,44)
      if ( == %tok) dispr @Info   - [userlist:color_remove]
      else {
        if ($remove(%tok,) isnum 0-99) {
          var %color = $ifmatch
          %ule-raw = $addtok(%ule-raw,=color $+ $_cprep(%color),44)
          dispr @Info   - [userlist:color_add:color=%tok $+ $_colorword(%color)]
        }
      }
    }
    if (%stuff != $null) goto edit
    %ule-raw = $_leveledit(%chan,%ule-raw,%newlvl)
    if (%ule-raw == $dlevel) .ruser %ule-mask
    else .auser %ule-raw %ule-mask
    _reorderblack
    scon -at1 _nickcol.updatemask %ule-mask 1
    if (%ule-nick) writeini $_cfg(userinfo.ini) %ule-mask nick %ule-nick
    return %ule-raw
  }
  ; convert locals to global for editor
  set -u %.ule-nick %ule-nick
  set -u %.ule-raw %ule-raw
  set -u %.ule-mask %ule-mask
  set -u %.mask %mask
  set -u %.rest %rest
  _cs-prep useredit | _dialog -am useredit useredit
}
alias _namedul {
  if (Â¬ isin $1) { var %chan = $gettok($1,1,172),%lvl = $gettok($1,2,172) }
  elseif (=* iswm $1) return
  else { var %chan = *,%lvl = $1 }
  if (=* iswm %chan) %chan = $right(%chan,-1)
  var %text,%num = $calc(%lvl)
  if (%num == 1) var %num
  else {
    if (%num < 25) %text = ([userlist:level_none:lower])
    elseif (%num < 50) %text = ([userlist:level_exempt:lower])
    elseif (%num < 60) %text = ([userlist:level_safe:lower])
    elseif (%num < 75) %text = ([userlist:level_protect:lower])
    elseif (%num < 175) %text = ([userlist:level_revenge:lower])
    else %text = ([userlist:level_master:lower])
    %num = $:t(%num)
  }
  var %spec,%pw = $readini($_cfg(userinfo.ini),n,$2,%chan)
  if ($right($1,1) == -) %spec = $:b([userlist:mode_deop]) $+ $iif(%num,$chr(44))
  elseif (*+ iswm $1) %spec = $:b([userlist:mode_voice]) ( $+ $iif(%pw == ?,[userlist:pw_unset],$iif(%pw,[userlist:pw_set],[userlist:pw_auto])) $+ ) $+ $iif(%num,$chr(44))
  elseif (*% iswm $1) %spec = $:b([userlist:mode_hop]) ( $+ $iif(%pw == ?,[userlist:pw_unset],$iif(%pw,[userlist:pw_set],[userlist:pw_auto])) $+ ) $+ $iif(%num,$chr(44))
  elseif (*@ iswm $1) %spec = $:b([userlist:mode_op]) ( $+ $iif(%pw == ?,[userlist:pw_unset],$iif(%pw,[userlist:pw_set],[userlist:pw_auto])) $+ ) $+ $iif(%num,$chr(44))
  elseif (%text == $null) %spec = [word:none]
  return $iif(%chan == *,[word:default],$:s(%chan)) [word:level:lower] - %spec %num %text
}

;
; Userlist editor
;

;;; import/export?
;;; last seen times?

; /userlist [channel|*|+]
; Shows all, just a channel, or just 'global'
; + means 'refresh' (if open)
alias userlist useredit $1
alias useredit {
  if ($1 == +) {
    if ($window(@Userlist) == $null) return
    if ($gettok($window(@Userlist).title,1-2,32) == ([userlist:default_only:lower])) userlist *
    elseif ($gettok($window(@Userlist).title,1,32) != -) userlist $right($left($ifmatch,-1),-1)
    elseif ($window(@Userlist).title) userlist
    return
  }
  if (($1) && ($1 != *) && (!$_ischan($1))) userlist $_patch($1)
  if ($window(@Userlist)) clear @Userlist
  else _window 2.6 -slzk -t20,85,91,106 @Userlist $_winpos(8,12,10,10) @Userlist
  var %who,%lvl,%nick,%color,%lvlbit,%bit,%spec,%lvlnum,%num = $ulist(*,0),%file = $_cfg(userinfo.ini)
  if (%num == 0) goto done
  :loop
  %who = $ulist(*,%num)
  %lvl = $remtok($remtok($remtok($level(%who),=black,44),=auth,44),=dcc,44)
  if (%lvl != $dlevel) {
    %nick = $readini(%file,n,%who,nick)
    if (%nick == $null) %nick =  
    %lvlbit = $numtok(%lvl,44)
    if ($wildtok(%lvl,=color*,1,44)) %color = $remove($ifmatch,=color)
    else var %color
    :lvlloop
    %bit = $gettok(%lvl,%lvlbit,44)
    if (Â¬ isin %bit) { %chan = $gettok(%bit,1,172) | %bit = $gettok(%bit,2,172) }
    elseif (=* iswm %bit) goto lvlnext
    elseif ((%bit == 1) && ((%color == $null) || (Â¬ isin %lvl))) goto lvlnext
    else %chan = *
    if (=* iswm %chan) %chan = $right(%chan,-1)
    if (($1) && ($1 != %chan)) goto lvlnext
    %lvlnum = $calc(%bit)
    if (%lvlnum < 2) %lvlnum =  
    if ($right(%bit,1) == -) %spec = [userlist:mode_deop]
    elseif (*+ iswm %bit) %spec = [userlist:mode_voice] $iif($readini(%file,n,%who,%chan),([userlist:pw_set_short]),([userlist:pw_auto_short]))
    elseif (*% iswm %bit) %spec = [userlist:mode_hop] $iif($readini(%file,n,%who,%chan),([userlist:pw_set_short]),([userlist:pw_auto_short]))
    elseif (*@ iswm %bit) %spec = [userlist:mode_op] $iif($readini(%file,n,%who,%chan),([userlist:pw_set_short]),([userlist:pw_auto_short]))
    else %spec =  
    if (%chan == *) var %chan | else %chan = ( $+ %chan $+ )
    aline %color @Userlist %nick $+ 	 $+ %who $+ 	 $+ %lvlnum $+ 	 $+ %spec $+ 	 $+ %chan
    :lvlnext
    if (%lvlbit > 1) { dec %lvlbit | goto lvlloop }
  }
  if (%num > 1) { dec %num | goto loop }
  :done
  %num = $line(@Userlist,0)
  iline @Userlist 1 [userlist:instructions1]
  iline @Userlist 2 [userlist:instructions2]
  iline @Userlist 3  
  iline @Userlist 4 [word:nickname]	[word:address]	[word:level]	[word:status]	[word:channel]
  iline @Userlist 5  
  window -b @Userlist
  if ($1 == *) titlebar @Userlist ([userlist:default_only:lower]) - %num [word:users:lower]
  elseif ($1) titlebar @Userlist ( $+ $1 $+ ) - %num [word:users:lower]
  else titlebar @Userlist - %num [word:users:lower]
}
alias -l _useredit {
  var %total,%num,%line,%chan,%mask
  :loop
  %num = $sline(@Userlist,1).ln
  if (%num) {
    %line = $sline(@Userlist,1)
    %mask = $gettok(%line,2,9)
    if ($gettok(%line,5,9) != $null) %chan = $right($left($ifmatch,-1),-1)
    else %chan = *
    if ($1 == ) {
      .user %mask  $+ $2
      scon -at1 _nickcol.updatemask %mask 1
      cline $2 @Userlist %num
      sline -r @Userlist %num
    }
    if ($1 == =) {
      if ($2 !isnum) return
      .user %mask %chan $2
      if ($wildtok($result,=color*,1,44)) _ridline $remove($ifmatch,=color) @Userlist %num $puttok(%line,$2,3,9)
      else _ridline @Userlist %num $puttok(%line,$2,3,9)
    }
    if ($1 == @) {
      .user %mask %chan $2-3
      if ($wildtok($result,=color*,1,44)) _ridline $remove($ifmatch,=color) @Userlist %num $puttok(%line,$4-,4,9)
      else _ridline @Userlist %num $puttok(%line,$4-,4,9)
    }
    if ($1 == r) {
      scon -at1 _nickcol.updatemask %mask 1
      .user -r %mask %chan
      dline @Userlist %num
    }
    if ($1 == e) { %total = $addtok(%total,%mask,44) | sline -r @Userlist %num }
    goto loop
  }
  if ($1 == e) { set -u %.selchan %chan | user %total }
  if ($1 == r) { %num = $line(@Userlist,0) - 5 | titlebar @Userlist $gettok($window(@Userlist).title,1- $+ $calc($numtok($window(@Userlist).title,32) - 2),32) %num [word:users:lower] }
}
alias -l _userfind {
  sline -r @Userlist
  var %num = $line(@Userlist,0)
  :loop
  if (%num > 5) {
    if ($1 == 3) { if ($gettok($line(@Userlist,%num),3,9) $2 $3) sline -a @Userlist %num }
    elseif ($2 iswm $gettok($line(@Userlist,%num),$1,9)) sline -a @Userlist %num
    dec %num | goto loop
  }
}

menu @Userlist {
  dclick:if ($sline($active,1).ln > 5) _useredit e
  [popups_userlist:add]...:user $_entry(-1,$null,[userlist:add_prompt])
  $iif($sline($active,1).ln > 5,[popups_userlist:remove]):_useredit r
  -
  $iif($sline($active,1).ln > 5,[popups_userlist:edit]...):_useredit e
  -
  $iif($sline($active,1).ln > 5,[popups_userlist:level])
  .1	[popups_userlist:none]:_useredit = 1
  .25	[popups_userlist:level_exempt]:_useredit = 25
  .50	[popups_userlist:level_safe]:_useredit = 50
  .60	[popups_userlist:level_protect]:_useredit = 60
  .75	[popups_userlist:level_revenge]:_useredit = 75
  .-
  .	[popups_userlist:level_custom]...:_useredit = $_entry(-2,$null,[userlist:level_prompt])
  $iif($sline($active,1).ln > 5,[popups_userlist:status])
  .@	[popups_userlist:status_op]:_useredit @ @ ! [userlist:mode_op] ([userlist:pw_auto_short])
  .@	[popups_userlist:status_op_pw]:_useredit @ @ ?? [userlist:mode_op] ([userlist:pw_set_short])
  .-
  .% $+ 	[popups_userlist:status_hop]:_useredit @ % ! [userlist:mode_hop] ([userlist:pw_auto_short])
  .% $+ 	[popups_userlist:status_hop_pw]:_useredit @ % ?? [userlist:mode_hop] ([userlist:pw_set_short])
  .-
  .+	[popups_userlist:status_voice]:_useredit @ + ! [userlist:mode_voice] ([userlist:pw_auto_short])
  .+	[popups_userlist:status_voice_pw]:_useredit @ + ?? [userlist:mode_voice] ([userlist:pw_set_short])
  .-
  .	[popups_userlist:none]:_useredit @ x !  
  .	[popups_userlist:status_deop]:_useredit @ - ! [userlist:mode_deop]
  [popups_userlist:color]
  .[popups_userlist:none]:_useredit 
  .-
  .[color:1:popups]	1:_useredit  01
  .[color:2:popups]	2:_useredit  02
  .[color:3:popups]	3:_useredit  03
  .[color:4:popups]	4:_useredit  04
  .[color:5:popups]	5:_useredit  05
  .[color:6:popups]	6:_useredit  06
  .[color:7:popups]	7:_useredit  07
  .[color:8:popups]	8:_useredit  08
  .[color:9:popups]	9:_useredit  09
  .[color:10:popups]	10:_useredit  10
  .[color:11:popups]	11:_useredit  11
  .[color:12:popups]	12:_useredit  12
  .[color:13:popups]	13:_useredit  13
  .[color:14:popups]	14:_useredit  14
  .[color:15:popups]	15:_useredit  15
  .[color:0:popups]	16:_useredit  16
  -
  [popups_userlist:view]
  .[popups_userlist:view_all]:userlist
  .-
  .$chan(1):userlist $chan(1)
  .$chan(2):userlist $chan(2)
  .$chan(3):userlist $chan(3)
  .$chan(4):userlist $chan(4)
  .$chan(5):userlist $chan(5)
  .-
  .[popups_userlist:view_chan]...:userlist $_entry(-1,$null,[userlist:find_chan_prompt])
  .[popups_userlist:view_default]:userlist *
  [popups_userlist:find]
  .[popups_userlist:find_nick]...:_userfind 1 $_entry(-1,$null,[userlist:find_nick_prompt])
  .[popups_userlist:find_addr]...:_userfind 2 $_entry(-1,$null,[userlist:find_addr_prompt])
  .-
  .[popups_userlist:find_above]...:_userfind 3 >= $_entry(-2,$null,[userlist:find_above_prompt])
  .[popups_userlist:find_below]...:_userfind 3 <= $_entry(-2,$null,[userlist:find_below_prompt])
  .[popups_userlist:find_equal]...:_userfind 3 == $_entry(-2,$null,[userlist:find_equal_prompt])
}

dialog useredit {
  title "[user_dialog:title]"
  icon script\pnp.ico
  option map
  size -1 -1 253 174

  text "&[word:nickname:dlg]:", 201, 4 8 40 12, right
  edit "", 1, 46 6 66 13, autohs
  text "([user_dialog:nickname_note:lower])", 202, 116 8 80 9

  icon 80, 201 7 10 9
  text "([user_dialog:color:lower])", 81, 216 4 30 18, center

  text "&[user_dialog:usermask]:", 203, 4 22 40 12, right
  combo 2, 46 19 167 79, drop sort edit

  text "&[dialog:editing]", 205, 6 44 29 12
  combo 71, 37 41 86 79, drop sort
  text "[word:level:dlg]:", 206, 126 44 44 12

  button "&[dialog:add]...", 56, 171 42 33 12
  button "&[dialog:del]", 57, 211 42 33 12

  box "[word:level:dlg:lower]:", 207, 6 60 126 82

  radio "1 - &[word:none:dlg]", 101, 13 72 113 9, group
  radio "25 - &[user_dialog:exempt]", 125, 13 82 113 9
  radio "50 - &[userlist:level_safe:dlg]", 150, 13 92 113 9
  radio "60 - &[userlist:level_protect:dlg]", 160, 13 101 113 9
  radio "75 - &[userlist:level_revenge:dlg]", 175, 13 111 113 9
  radio "&[dialog:custom]:", 100, 13 126 53 9
  edit "100", 61, 66 124 26 13

  box "[user_dialog:status]:", 40, 146 60 100 82

  check "&[userlist:mode_deop:dlg]", 21, 153 72 86 9, group
  check "&[userlist:mode_voice:dlg]", 22, 153 82 86 9
  check "&[userlist:mode_hop:dlg]", 23, 153 92 86 9
  check "&[userlist:mode_op:dlg]", 24, 153 101 86 9
  check "&[user_dialog:password]:", 31, 153 114 86 9
  edit "", 32, 162 124 76 13, autohs pass

  button "[dialog:ok]", 52, 6 153 53 14, OK
  button "[dialog:cancel]", 53, 68 153 53 14, cancel
  button "&[dialog:remove]", 54, 130 153 53 14
  button "&[dialog:help]", 55, 192 153 53 14, disable

  edit "", 210, 1 1 1 1, hide autohs
  edit "", 211, 1 1 1 1, hide autohs
  list 212, 1 1 1 1, hide
  edit "", 213, 1 1 1 1, hide autohs
  edit "", 214, 1 1 1 1, hide autohs
}
on *:DIALOG:useredit:init:*:{
  if ($wildtok(%.ule-raw,=color*,1,44)) hadd pnp.dlgcolor. $+ $dname 80 $calc($remove($ifmatch,=color) % 16)
  else hadd pnp.dlgcolor. $+ $dname 80 16
  did -g $dname 80 script\ $+ $hget(pnp.dlgcolor. $+ $dname,80) $+ .bmp
  did -a $dname 214 %.rest
  did -ac $dname 2 %.ule-mask
  if (%.ule-mask != %.mask) _ddaddm $dname 2 %.mask 3 4 010 2 011
  if (%.ule-nick) did -a $dname 1 $ifmatch
  elseif ((* !isin %.mask) && (! !isin %.mask) && (@ !isin %.mask) && (. !isin %.mask)) did -a $dname 1 %.mask
  did -ac $dname 71 ( $+ [word:default] $+ )
  did -a $dname 210 %.ule-raw
  if (%.ule-raw == $dlevel) did -b $dname 54
  else did -a $dname 211 %.ule-mask
  var %chan,%lvl,%num = $numtok(%.ule-raw,44),%file = $_cfg(userinfo.ini)
  :lvlloop
  %lvl = $gettok(%.ule-raw,%num,44)
  if (Â¬ isin %lvl) {
    %chan = $gettok(%lvl,1,172)
    if (=* iswm %chan) %chan = $right(%chan,-1)
    did -a $+ $iif((%chan == $active) || (%chan == %.selchan),c) $dname 71 %chan
    if ($readini(%file,n,%.ule-mask,%chan)) did -a $dname 212 %chan $ifmatch
  }
  if (%num > 1) { dec %num | goto lvlloop }
  if ($active ischan) _ddadd $dname 71 $active
  if ($readini(%file,n,%.ule-mask,*)) did -a $dname 212 * $ifmatch
  if ($left($did(71),1) == $chr(40)) did -b $dname 57
  unset %.ule-mask %.ule-raw %.ule-nick %.rest %.mask
  _uefreshen
}
on *:DIALOG:useredit:sclick:80:_cs-go 15 15 1
on *:DIALOG:useredit:sclick:54:{
  _cs-fin $dname
  remini $_cfg(userinfo.ini) $did(211)
  .ruser $did(211)
  scon -at1 _nickcol.updatemask $did(211) 1
  if ($did(214)) .timer -mio 1 0 user $did(214)
  else userlist +
  dialog -x $dname
}
on *:DIALOG:useredit:sclick:53:_cs-fin $dname | userlist +
on *:DIALOG:useredit:sclick:52:{
  if ($did(2) == $null) _error [error:cant_add_user]
  _uesave
  var %line,%file = $_cfg(userinfo.ini),%num = $did(212).lines
  if ($did(211)) {
    remini %file $did(211)
    .ruser $did(211)
    scon -at1 _nickcol.updatemask $did(211) 1
  }
  remini %file $did(2)
  if ($did(1)) writeini %file $did(2) nick $did(1)
  :loop
  if (%num) {
    %line = $did(212,%num)
    if ($gettok(%line,2,32)) writeini %file $did(2) %line
    dec %num | goto loop
  }
  if ($wildtok($did(210),=color*,1,44)) did -o $dname 210 1 $remtok($did(210),$ifmatch,1,44)
  if ($hget(pnp.dlgcolor. $+ $dname,80) < 16) did -o $dname 210 1 $addtok($did(210),=color $+ $_cprep($ifmatch),44)
  if ($did(210) == $dlevel) .ruser $did(2)
  else .auser $did(210) $did(2)
  _reorderblack
  scon -at1 _nickcol.updatemask $did(2) 1
  _cs-fin $dname
  if ($did(214)) .timer -mio 1 0 user $did(214)
  else userlist +
}
on *:DIALOG:useredit:sclick:56:{
  var %newchan = $_entry(-1,$comchan($did(useredit,1),1),[userlist:chan_prompt])
  if ((!$_ischan(%newchan)) || ($chr(44) isin %newchan)) _error [error:invalid_chan]
  _uesave
  if ($_finddid(useredit,71,%newchan)) {
    did -c useredit 71 $ifmatch
    if ($_chanlevel(%newchan,$did(useredit,210)) == $null) did -o useredit 210 1 $_leveledit(%newchan,$did(useredit,210),$calc($_level(*,$did(useredit,210))))
  }
  else {
    did -o useredit 210 1 $_leveledit(%newchan,$did(useredit,210),$calc($_level(*,$did(useredit,210))))
    did -ac useredit 71 %newchan
  }
  _uefreshen
}
on *:DIALOG:useredit:sclick:57:{
  did -u $dname 31
  _uesave
  did -o $dname 210 1 $_leveledit($did(71),$did(210))
  did -d $dname 71 $did(71).sel
  did -c $dname 71 $did(71).lines
  _uefreshen
}
on *:DIALOG:useredit:sclick:71:_uesave | _uefreshen | if ($left($did(71),-1) == $chr(40)) did -b $dname 57 | else did -e $dname 57
on *:DIALOG:useredit:sclick:100:did -e $dname 61
on *:DIALOG:useredit:sclick:31:did $iif($did(31).state,-e,-b) $dname 32
on *:DIALOG:useredit:sclick:*:{
  if ($did > 100) did -b $dname 61
  elseif ($did isnum 21-24) {
    if ($did != 21) did -u $dname 21
    if ($did != 22) did -u $dname 22
    if ($did != 23) did -u $dname 23
    if ($did != 24) did -u $dname 24
    if (($did(24).state) || ($did(23).state) || ($did(22).state)) { did -e $dname 31 | if ($did(31).state) did -e $dname 32 }
    else did -b $dname 31,32
  }
}
alias -l _uesave {
  var %lvl,%op,%pw,%chan = $iif($left($did(useredit,213),1) == $chr(40),*,$did(useredit,213))
  if ($did(useredit,101).state) { did -u useredit 101 | %lvl = 1 }
  elseif ($did(useredit,125).state) { did -u useredit 125 | %lvl = 25 }
  elseif ($did(useredit,150).state) { did -u useredit 150 | %lvl = 50 }
  elseif ($did(useredit,160).state) { did -u useredit 160 | %lvl = 60 }
  elseif ($did(useredit,175).state) { did -u useredit 175 | %lvl = 75 }
  else {
    did -u useredit 100
    if ($did(useredit,61) isnum) %lvl = $did(useredit,61)
    else %lvl = 1
  }
  if ($did(useredit,21).state) { did -u useredit 21 | %op = - }
  elseif ($did(useredit,22).state) { did -u useredit 22 | %op = + }
  elseif ($did(useredit,23).state) { did -u useredit 23 | %op = % }
  elseif ($did(useredit,24).state) { did -u useredit 24 | %op = @ }
  if (%op isin +%@) {
    if ($did(useredit,31).state) {
      if ($did(useredit,32) == $null) %pw = ?
      elseif ($left($did(useredit,32),1) isletter) %pw = $hash($did(useredit,32),32)
      else %pw = ??
    }
  }
  if (%pw == $null) { if ($_finddid(useredit,212,%chan)) did -d useredit 212 $ifmatch }
  elseif (%pw != ??) {
    if ($_finddid(useredit,212,%chan)) did -o useredit 212 $ifmatch %chan %pw
    else did -a useredit 212 %chan %pw
  }
  %lvl = %lvl $+ %op
  if ((%lvl == $_level(*,$did(useredit,210))) && (%chan != *) && ($_chanlevel(%chan,$did(useredit,210)) == $null)) return
  did -o useredit 210 1 $_leveledit(%chan,$did(useredit,210),%lvl)
}
alias -l _uefreshen {
  did -o useredit 213 1 $did(useredit,71)
  var %lvl = $_level($did(useredit,71),$did(useredit,210))
  if ($istok(1.25.50.60.75,$calc(%lvl),46)) { did -c useredit $calc(100 + $calc(%lvl)) | did -b useredit 61 }
  else { did -c useredit 100 | did -oe useredit 61 1 $calc(%lvl) }
  %lvl = $pos(-+%@,$right(%lvl,1))
  if (%lvl) did -c useredit $calc(20 + %lvl)
  if ((%lvl == $null) || (%lvl < 2)) { did -bu useredit 31 | did -br useredit 32 }
  else {
    if ($_finddid(useredit,212,$iif($left($did(useredit,71),1) == $chr(40),*,$did(useredit,71)))) {
      var %pw = $gettok($did(useredit,212,$ifmatch),2,32)
      did -ec useredit 31
      did -er useredit 32
      if (%pw != ?) did -a useredit 32 (encrypted)
    }
    else { did -eu useredit 31 | did -br useredit 32 }
  }
}

;
; Userlist CTCP commands
;

; OPME|HOPME|VOICEME #chan [pw]
ctcp &*:OPME:?:_doopc *@ *@ *@ o OP [public_opctcp:action_op:lower:s2p] $2-
ctcp &*:HOPME:?:_doopc *% *@ *@ h HOP [public_opctcp:action_hop:lower:s2p] $2-
ctcp &*:VOICEME:?:_doopc *+ *% *@ v VOICE [public_opctcp:action_voice:lower:s2p] $2-
alias -l _doopc {
  var %error,%chan = $7
  var %level = $level($fulladdress)
  if (($wildtok(%level,$1,0,44) == 0) && ($wildtok(%level,$2,0,44) == 0) && ($wildtok(%level,$3,0,44) == 0)) return
  var %pw = $hash($8,32)
  var %sublvl = $_level($7,%level)
  hinc -u30 pnp.flood. $+ $cid opme. $+ $site
  if ($hget(pnp.flood. $+ $cid,opme. $+ $site) > 3) {
    _linedance _qnotice $nick [public_opctcp:flood]
    hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
    %error = [userlist:error_flood:lower]
  }
  elseif (!$_ischan($7)) {
    _linedance _qnotice $nick [public_opctcp:syntax]-  $+ $5 $+ ME #chan [[public_opctcp:password:lower]]
    %error = [userlist:error_invalid:lower]
    %chan = ?????
  }
  elseif (($1 iswm %sublvl) || ($2 iswm %sublvl) || ($3 iswm %sublvl)) {
    var %scan = $chr(44) $+ = $+ $7 $+ Â¬
    if (%scan isin %level) %scan = $7
    else %scan = *
    var %thepw = $readini($_cfg(userinfo.ini),n,$maddress,%scan)
    if (%thepw == ?) {
      var %cmd = PASS [#chan] [public_opctcp:new:lower]
      _linedance _qnotice $nick [public_opctcp:no_password:cmd=%cmd]
      %error = [userlist:error_nopw:lower]
    }
    elseif ((%thepw == $null) || (%thepw == %pw)) {
      if (($me isop $7) || (($me ishop $7) && ($3 == v))) { _init.mass $7 4 | _add.mass $7 + $4 $nick }
      else {
        _linedance _qnotice $nick [public_opctcp:no_ops:action=$_p2s($6):chan=$7]
        %error = [userlist:error_noop:lower]
      }
    }
    else {
      _linedance _qnotice $nick [public_opctcp:password_wrong]
      hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
      %error = [userlist:error_wrongpw:lower]
    }
  }
  else {
    _linedance _qnotice $nick [public_opctcp:no_access:chan=$7]
    %error = [userlist:error_noaccess:lower]
  }
  if (%error) disptc -s %chan [userlist:opctcp_failed:ctcp=$5 $+ ME:nick=$;t($nick):error=%error]
  else disptc -s $7 [userlist:opctcp_granted:nick=$;t($nick):ctcp=$5 $+ ME]
  halt
}
; HELP
ctcp &*:HELP:?:{
  var %level = $level($fulladdress),%op = $wildtok(%level,*@,0,44),%hop = $wildtok(%level,*%,0,44),%voc = $wildtok(%level,*+,0,44)
  if ((%op == 0) && (%hop == 0) && (%voc == 0)) return
  hinc -u20 pnp.flood. $+ $cid opme. $+ $site
  if ($hget(pnp.flood. $+ $cid,opme. $+ $site) > 2) {
    _linedance _qnotice $nick [public_opctcp:flood]
    hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
    var %error = [userlist:error_flood:lower]
    disps [userlist:opctcp_failed:ctcp=HELP:nick=$;t($nick):error=%error]
  }
  else {
    if (%op) {
      var %cmd = OPME #chan [[public_opctcp:password:lower]]
      _linedance _qnotice $nick [public_opctcp:help1:cmd=%cmd]
    }
    if (%hop) {
      var %cmd = HOPME #chan [[public_opctcp:password:lower]]
      _linedance _qnotice $nick [public_opctcp:help1:cmd=%cmd]
    }
    if (%voc) {
      var %cmd = VOICEME #chan [[public_opctcp:password:lower]]
      _linedance _qnotice $nick [public_opctcp:help2:cmd=%cmd]
    }
    var %cmd = PASS [#chan] [[public_opctcp:old:lower]] [public_opctcp:new:lower]
    _linedance _qnotice $nick [public_opctcp:help3:cmd=%cmd]
    disps [userlist:opctcp_granted:nick=$;t($nick):ctcp=HELP]
  }
  halt
}
; PASS [#chan] [old] new
ctcp &*:PASS:?:{
  var %level = $level($fulladdress),%error
  if (($wildtok(%level,*@,0,44) == 0) && ($wildtok(%level,*%,0,44) == 0) && ($wildtok(%level,*+,0,44) == 0)) return
  hinc -u30 pnp.flood. $+ $cid opme. $+ $site
  if ($hget(pnp.flood. $+ $cid,opme. $+ $site) > 3) {
    _linedance _qnotice $nick [public_opctcp:flood]
    hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
    %error = [userlist:error_flood:lower]
    %chan = *
  }
  elseif ($2 == $null) {
    _linedance _qnotice $nick [public_opctcp:syntax]- PASS [#chan] [[public_opctcp:old:lower]] [public_opctcp:new:lower]
    %error = [userlist:error_invalid:lower]
    %chan = *
  }
  else {
    if ($_ischan($2)) var %chan = $2,%old = $3,%new = $4
    else var %chan = *,%old = $2,%new = $3
    if (%new == $null) { %new = %old | %old = ? }
    else %old = $hash(%old,32)
    if (($left(%new,1) !isletter) && ($left(%new,1) !isnum)) {
      _linedance _qnotice $nick [public_opctcp:invalid_password1]
      %error = [userlist:error_pwinvalid:lower]
    }
    elseif ($len(%new) < 5) {
      _linedance _qnotice $nick [public_opctcp:invalid_password2]
      %error = [userlist:error_pwshort:lower]
    }
    else {
      var %num = $numtok(%level,44),%changes = 0
      :loop
      var %sub = $gettok(%level,%num,44)
      if (((%num == 1) || (=*Â¬* iswm %sub)) && ($right(%sub,1) isin +%@)) {
        if (%num == 1) var %subc = *
        else var %subc = $right($gettok(%sub,1,172),-1)
        var %oldpw = $readini($_cfg(userinfo.ini),n,$maddress,%subc)
        if (((%subc == %chan) || (%chan == *)) && (%oldpw == %old)) {
          writeini $_cfg(userinfo.ini) $maddress %subc $hash(%new,32)
          inc %changes
        }
        elseif ((%subc == %chan) && (%chan != *)) {
          _linedance _qnotice $nick [public_opctcp:password_wrong]
          hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
          %error = [userlist:error_wrongpw:lower]
        }
      }
      if (%num > 1) { dec %num | goto loop }
      if (%chan != *) {
        if (%changes) _linedance _qnotice $nick [public_opctcp:password_changed:password= $+ %new $+ ]
        elseif (%error == $null) {
          _linedance _qnotice $nick [public_opctcp:no_access:chan=%chan]
          %error = [userlist:error_noaccess:lower]
        }
      }
      else {
        if (%changes) _linedance _qnotice $nick [public_opctcp:password_changed:password= $+ %new $+ ] ([public_opctcp:changes:num=%changes])
        else {
          _linedance _qnotice $nick [public_opctcp:password_wrong]
          hinc -u30 pnp.flood. $+ $cid ignore.ctcp. $+ $site
          %error = [userlist:error_wrongpw:lower]
        }
      }
    }
  }
  if (%chan == *) %chan = ?????
  if (%error) disptc -s %chan [userlist:opctcp_failed:ctcp=PASS:nick=$;t($nick):error=%error]
  else disptc -s %chan [userlist:opctcp_passchange:nick=$;t($nick):num=%changes]
  halt
}

